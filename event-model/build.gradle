apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.starchartlabs.flare.managed-credentials'
apply plugin: 'org.starchartlabs.flare.metadata-base'
apply plugin: 'org.starchartlabs.flare.metadata-pom'

description='Models events which may be sent to the Major Tom system by external clients'

//Dependency versions managed in $rootDir/dependencies.properties
dependencies {
    compile 'com.google.code.findbugs:jsr305'
    compile 'com.google.code.gson:gson'
    compile 'org.starchartlabs.alloy:alloy-core'
}

credentials {
    bintray {
        environment 'BINTRAY_USER', 'BINTRAY_API_KEY'
    }
}

projectMetaData {
    github {
        repository 'StarChart-Labs', 'major-tom'
        
        developers file("${rootDir}/developers.properties")
    }
    
    licenses {
        mit()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

// If not a remote run, publish to local only
if(!project.hasProperty('remoteDeploy')){
    publishing {
        repositories {
            mavenLocal()
        }
    }
}

// TODO romeara Workaround to lack of defaults on published POM for name and description, until added in flare-plugins
project.afterEvaluate { p ->
    publishing.publications.withType(MavenPublication.class).all { publication ->
        publication.pom {
            name = "${project.name}"
            description = "${project.description}"
        }
    }
}

bintray {
    publications = [ 'maven' ]
    user = System.getProperty('bintray_user')
    key = System.getProperty('bintray_key')

    if(!project.hasProperty('remoteDeploy')){
        dryRun = true
    }

    pkg {
        repo = 'major-tom'
        name = "${project.name}"
        userOrg = 'starchart-labs'
        licenses = ['MIT']
        vcsUrl = "${projectMetaData.scm.vcsUrl}"

        version {
            name = "${project.version}"
            desc = "${project.name} release ${project.version} final"
            released  = new Date()
            vcsTag = "${project.version}"

            gpg {
                sign = true
            }
        }
    }
}
